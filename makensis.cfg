; ------------------------------------------------------------
; Skript  : Freizeitkarte_TWN.nsi
; Version : 1.3.12 - 2016/03/11
; Erzeugt : Fri Sep 30 12:36:55 2016
;
; Bemerkungen:
; - Kopieren der Kartendateien
; - Eintragen der Windows-Registry-Keys fur die Kartennutzung
; - Eintragen der Windows-Registry-Keys fur die Deinstallation
; - Kopieren des Deinstallationsprogramms
; ------------------------------------------------------------

; General Settings
; ----------------

; Installationsverzeichnis (Default)
!define INSTALLATIONS_VERZEICHNIS "C:\Taiwan_TOPO\__name_word__"

; Beschreibung der Karte
!define KARTEN_BESCHREIBUNG "__name_word__"

; Ausgabe der Karte
!define KARTEN_AUSGABE "v__version__"

; Name der Installer-EXE-Datei
!define INSTALLER_EXE_NAME "Install___name_word__"

; Name der Karte
!define MAPNAME "__name_word__"

; Product-ID der Karte
!define PRODUCT_ID "1"

; Name des Windows-Registrierungsschlussels
!define REG_KEY "__name_word__"

; Name des alten Windows-Registrierungsschlussels (vor Umbenennung der Karten)
!define REG_KEY_OLD "no_old_name"

; Name des kartenspezifischen TYP-Files
!define TYPNAME "__mapid__.TYP"


; Compressor Settings
; -------------------
SetCompress off


; Include Modern UI
; -----------------
!include "MUI2.nsh"


; Interface Settings
; ------------------
!define MUI_LANGDLL_ALLLANGUAGES


; Installer Pages
; ---------------

!define MUI_WELCOMEPAGE_TITLE_3LINES
!define MUI_WELCOMEPAGE_TITLE "$(INWpTitle)"
!define MUI_WELCOMEPAGE_TEXT "$(INWpText)"

!define MUI_FINISHPAGE_TITLE_3LINES
!define MUI_FINISHPAGE_TITLE "$(INFpTitle)"
!define MUI_FINISHPAGE_TEXT "$(INFpText)"

!define MUI_WELCOMEFINISHPAGE_BITMAP Install.bmp

!insertmacro MUI_PAGE_WELCOME
!insertmacro MUI_PAGE_LICENSE $(licenseFile)
!insertmacro MUI_PAGE_DIRECTORY
!insertmacro MUI_PAGE_INSTFILES
!insertmacro MUI_PAGE_FINISH


; Init Routine
; ------------

!define MUI_CUSTOMFUNCTION_GUIINIT myGuiInit


; Uninstaller Pages
; -----------------
!define MUI_WELCOMEPAGE_TITLE_3LINES
!define MUI_WELCOMEPAGE_TITLE "$(UIWpTitle)"
!define MUI_WELCOMEPAGE_TEXT "$(UIWpText)"

!define MUI_FINISHPAGE_TITLE_3LINES
!define MUI_FINISHPAGE_TITLE "$(UIFpTitle)"
!define MUI_FINISHPAGE_TEXT "$(UIFpText)"

!define MUI_UNWELCOMEFINISHPAGE_BITMAP Deinstall.bmp

!insertmacro MUI_UNPAGE_WELCOME
!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES
!insertmacro MUI_UNPAGE_FINISH


; Language Settings
; -----------------
!insertmacro MUI_LANGUAGE "English"
!insertmacro MUI_LANGUAGE "German"

LangString INWpTitle ${LANG_ENGLISH} "Installation of ${KARTEN_BESCHREIBUNG} ${KARTEN_AUSGABE}"
LangString INWpTitle ${LANG_GERMAN} "Installation der ${KARTEN_BESCHREIBUNG} ${KARTEN_AUSGABE}"
LangString INWpText ${LANG_ENGLISH} "This Wizard will be guiding you through the installation of ${KARTEN_BESCHREIBUNG} ${KARTEN_AUSGABE} begleiten.$\n$\nBefore installation BaseCamp must be closed for allowing installation of the map data.$\n$\nChoose Next for starting the installation."
LangString INWpText ${LANG_GERMAN} "Dieser Assistent wird Sie durch die Installation der ${KARTEN_BESCHREIBUNG} ${KARTEN_AUSGABE} begleiten.$\n$\nVor der Installation muss das Programm BaseCamp geschlossen werden damit Kartendateien ersetzt werden koennen.$\n$\nKlicken Sie auf Weiter um mit der Installation zu beginnen."

LicenseLangString licenseFile ${LANG_ENGLISH} "lizenz_haftung_erstellung_en.txt"
LicenseLangString licenseFile ${LANG_GERMAN} "lizenz_haftung_erstellung.txt"

LangString INFpTitle ${LANG_ENGLISH} "Installation of ${KARTEN_BESCHREIBUNG} ${KARTEN_AUSGABE} finished"
LangString INFpTitle ${LANG_GERMAN} "Installation der ${KARTEN_BESCHREIBUNG} ${KARTEN_AUSGABE} abgeschlossen"
LangString INFpText ${LANG_ENGLISH} "${KARTEN_BESCHREIBUNG} ${KARTEN_AUSGABE} has been succesfully installed on your computer.$\n$\nHave fun using the map.$\n$\nFor ensuring and increasing the quality of the map also your feedback is helpful (e.g. defects or improvements). Already now many thanks for it.$\n$\nChoose Finish to terminate the installation."
LangString INFpText ${LANG_GERMAN} "Die ${KARTEN_BESCHREIBUNG} ${KARTEN_AUSGABE} wurde erfolgreich auf Ihrem Computer installiert.$\n$\nViel Erfolg und Freude bei der Nutzung der Karte.$\n$\nUm die Qualitaet dieser Karte zu sichern und zu verbessern ist auch Ihr Feedback (z.B. zu Defekten oder Verbesserungen) hilfreich. An dieser Stelle schon einmal Danke hierfuer.$\n$\nKlicken Sie auf Fertig stellen um den Assistenten zu beenden und die Installation abzuschliessen."

LangString UIWpTitle ${LANG_ENGLISH} "Deinstalling ${KARTEN_BESCHREIBUNG} ${KARTEN_AUSGABE}"
LangString UIWpTitle ${LANG_GERMAN} "Entfernen der ${KARTEN_BESCHREIBUNG} ${KARTEN_AUSGABE}"
LangString UIWpText ${LANG_ENGLISH} "This Wizard will be guiding you through the deinstallation of ${KARTEN_BESCHREIBUNG} ${KARTEN_AUSGABE}.$\n$\nBefore deinstallation BaseCamp must be closed for allowing deletion of the map data.$\n$\nChoose Next for starting the deinstallation."
LangString UIWpText ${LANG_GERMAN} "Dieser Assistent wird Sie durch die Deistallation der ${KARTEN_BESCHREIBUNG} ${KARTEN_AUSGABE} begleiten.$\n$\nVor dem Entfernen muss das Programm BaseCamp geschlossen werden damit Kartendateien geloescht werden koennen.$\n$\nKlicken Sie auf Weiter um mit der Deinstallation zu beginnen."

LangString UIFpTitle ${LANG_ENGLISH} "Deinstallation of ${KARTEN_BESCHREIBUNG} ${KARTEN_AUSGABE} finished"
LangString UIFpTitle ${LANG_GERMAN} "Entfernen der ${KARTEN_BESCHREIBUNG} ${KARTEN_AUSGABE} abgeschlossen"
LangString UIFpText ${LANG_ENGLISH} "${KARTEN_BESCHREIBUNG} ${KARTEN_AUSGABE} has been succesfully deinstalled from your computer.$\n$\nChoose Finish to terminate the deinstallation."
LangString UIFpText ${LANG_GERMAN} "Die ${KARTEN_BESCHREIBUNG} ${KARTEN_AUSGABE} wurde erfolgreich von Ihrem Computer entfernt.$\n$\nKlicken Sie auf Fertig stellen um den Assistenten zu beenden und die Deinstallation abzuschliessen."

LangString AlreadyInstalled ${LANG_ENGLISH} "There is already a version of ${KARTEN_BESCHREIBUNG} installed.$\nThis version needs to be deinstalled first."
LangString AlreadyInstalled ${LANG_GERMAN} "Es ist bereits eine Version der ${KARTEN_BESCHREIBUNG} installiert.$\nDiese Version muss zunaechst entfernt werden."
LangString AlreadyInstalledOldName ${LANG_ENGLISH} "There is already a version of ${KARTEN_BESCHREIBUNG} installed.$\n(still using the old name ${REG_KEY_OLD})$\nThis version needs to be deinstalled first."
LangString AlreadyInstalledOldName ${LANG_GERMAN} "Es ist bereits eine Version der ${KARTEN_BESCHREIBUNG} installiert.$\n(noch mit altem Namen ${REG_KEY_OLD})$\nDiese Version muss zunaechst entfernt werden."


; Initialize NSI-Variables
; ------------------------

; Uninstall key: DisplayName - Name of the application
Name "${KARTEN_BESCHREIBUNG} ${KARTEN_AUSGABE}"

; Installer-EXE
OutFile "${INSTALLER_EXE_NAME}.exe"

; Installationsverzeichnis
InstallDir "${INSTALLATIONS_VERZEICHNIS}"


Function myGUIInit

  ; Call the language selection dialog
  ; -------------------------------------------
  ;!insertmacro MUI_LANGDLL_DISPLAY

  ; Uninstall before Installing (actual mapname)
  ; -------------------------------------------
  ReadRegStr $R0 HKLM \
  "Software\Microsoft\Windows\CurrentVersion\Uninstall\${REG_KEY}" \
  "UninstallString"

  StrCmp $R0 "" noactualcard

  MessageBox MB_OKCANCEL|MB_ICONEXCLAMATION "$(AlreadyInstalled)" IDOK uninstactualcard
  Abort

  ; Run the Uninstaller
  ; -------------------
  uninstactualcard:
  Exec $R0

  noactualcard:

  ; Uninstall before Installing (old mapname)
  ; -------------------------------------------
  ReadRegStr $R0 HKLM \
  "Software\Microsoft\Windows\CurrentVersion\Uninstall\${REG_KEY_OLD}" \
  "UninstallString"

  StrCmp $R0 "" nooldcard

  MessageBox MB_OKCANCEL|MB_ICONEXCLAMATION "$(AlreadyInstalledOldName)" IDOK uninstoldcard
  Abort

  ; Run the Uninstaller
  ; -------------------
  uninstoldcard:
  Exec $R0

  nooldcard:

FunctionEnd


; Installer Section
; -----------------

Section "MainSection" SectionMain

  ; Get and create a temporary directory
  ; ------------------------------------
  Var /Global MyTempDir
  GetTempFileName $MyTempDir
  Delete $MyTempDir
  CreateDirectory $MyTempDir

  ; Files to be installed
  ; ---------------------
  SetOutPath "$MyTempDir"
  File "${MAPNAME}_InstallFiles.zip"
  !addplugindir "/Users/rudychung/Works/fzk-mde-garmin/Freizeitkarte-Entwicklung\tools\NSIS\windows\Plugins"
  nsisunz::UnzipToLog "$MyTempDir\${MAPNAME}_InstallFiles.zip" "$MyTempDir"
  Pop $0
  StrCmp $0 "success" +2
    call InstallError

  ; delete unpacked zip file again to safe space
  Delete "$MyTempDir\${MAPNAME}_InstallFiles.zip"

  ; Clear Errors and continue
  ClearErrors

  ; Create the Install Directory
  CreateDirectory "$INSTDIR"

  ; Copy TYP and other files
  CopyFiles "$MyTempDir\${MAPNAME}.img" "$INSTDIR\${MAPNAME}.img"
  Delete "$MyTempDir\${MAPNAME}.img"
  CopyFiles "$MyTempDir\${MAPNAME}_mdr.img" "$INSTDIR\${MAPNAME}_mdr.img"
  Delete "$MyTempDir\${MAPNAME}_mdr.img"
  CopyFiles "$MyTempDir\${MAPNAME}.mdx" "$INSTDIR\${MAPNAME}.mdx"
  Delete "$MyTempDir\${MAPNAME}.mdx"
  CopyFiles "$MyTempDir\6158.TYP" "$INSTDIR\6158.TYP"
  Delete "$MyTempDir\6158.TYP"
  CopyFiles "$MyTempDir\${MAPNAME}.tdb" "$INSTDIR\${MAPNAME}.tdb"
  Delete "$MyTempDir\${MAPNAME}.tdb"

  ; Copy the tiles,  __copy_tiles__

  ; Check for errors
  IfErrors 0 +2
    Call InstallError

  ; Delete temporary directory and content
  ; --------------------------------------
  RMDir /r $MyTempDir


  ; Create BaseCamp / MapSource registry keys
  ; -----------------------------------------
  WriteRegBin HKLM "SOFTWARE\Garmin\MapSource\Families\${REG_KEY}" "ID" __mapid_hex__

  WriteRegStr HKLM "SOFTWARE\Garmin\MapSource\Families\${REG_KEY}" "IDX" "$INSTDIR\${MAPNAME}.mdx"
  WriteRegStr HKLM "SOFTWARE\Garmin\MapSource\Families\${REG_KEY}" "MDR" "$INSTDIR\${MAPNAME}_mdr.img"

  WriteRegStr HKLM "SOFTWARE\Garmin\MapSource\Families\${REG_KEY}" "TYP" "$INSTDIR\${TYPNAME}"

  WriteRegStr HKLM "SOFTWARE\Garmin\MapSource\Families\${REG_KEY}\${PRODUCT_ID}" "BMAP" "$INSTDIR\${MAPNAME}.img"
  WriteRegStr HKLM "SOFTWARE\Garmin\MapSource\Families\${REG_KEY}\${PRODUCT_ID}" "LOC" "$INSTDIR"
  WriteRegStr HKLM "SOFTWARE\Garmin\MapSource\Families\${REG_KEY}\${PRODUCT_ID}" "TDB" "$INSTDIR\${MAPNAME}.tdb"

  ; Write uninstaller
  ; -----------------
  WriteUninstaller "$INSTDIR\Uninstall.exe"

  ; Create uninstaller registry keys
  ; --------------------------------
  WriteRegStr HKLM "SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\${REG_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr HKLM "SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\${REG_KEY}" "UninstallString" "$INSTDIR\Uninstall.exe"
  WriteRegDWORD HKLM "SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\${REG_KEY}" "NoModify" 1

SectionEnd


; Uninstaller Section
; -------------------

Section "Uninstall"

  ; Files to be uninstalled
  ; -----------------------
  Delete "$INSTDIR\${MAPNAME}.img"
  Delete "$INSTDIR\${MAPNAME}_mdr.img"
  Delete "$INSTDIR\${MAPNAME}.mdx"
  Delete "$INSTDIR\6158.TYP"
  Delete "$INSTDIR\${MAPNAME}.tdb"
  ; __delete_tiles__
  Delete "$INSTDIR\Uninstall.exe"

  RmDir "$INSTDIR"

  ; Registry cleanup
  ; ----------------
  DeleteRegValue HKLM "SOFTWARE\Garmin\MapSource\Families\${REG_KEY}" "ID"

  DeleteRegValue HKLM "SOFTWARE\Garmin\MapSource\Families\${REG_KEY}" "IDX"
  DeleteRegValue HKLM "SOFTWARE\Garmin\MapSource\Families\${REG_KEY}" "MDR"

  DeleteRegValue HKLM "SOFTWARE\Garmin\MapSource\Families\${REG_KEY}" "TYP"

  DeleteRegValue HKLM "SOFTWARE\Garmin\MapSource\Families\${REG_KEY}\${PRODUCT_ID}" "BMAP"
  DeleteRegValue HKLM "SOFTWARE\Garmin\MapSource\Families\${REG_KEY}\${PRODUCT_ID}" "LOC"
  DeleteRegValue HKLM "SOFTWARE\Garmin\MapSource\Families\${REG_KEY}\${PRODUCT_ID}" "TDB"
  DeleteRegKey /IfEmpty HKLM "SOFTWARE\Garmin\MapSource\Families\${REG_KEY}\${PRODUCT_ID}"
  DeleteRegKey /IfEmpty HKLM "SOFTWARE\Garmin\MapSource\Families\${REG_KEY}"

  DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${REG_KEY}"

SectionEnd


Function InstallError
  DetailPrint "$0"
  RMDir /r $MyTempDir
  Abort
FunctionEnd
